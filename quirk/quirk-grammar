<Program> -> <Statement> <Program> | <Statement>

<Statement> -> <FunctionDeclaration> | <Assignment> | <Print>


//function declaration statements
<FunctionDeclaration> -> FUNCTION <Name> <FunctionParams> LBRACE <FunctionBody> RBRACE

<FunctionParams> -> LPAREN RPAREN | LPAREN <NameList> RPAREN

<FunctionBody> -> <Return> | <Program> <Return>

<Return> -> RETURN <ParameterList>


//Assignment statements
<Assignment> -> <SingleAssignment> | <MultipleAssignment>

<SingleAssignment> -> VAR <Name> ASSIGN <Expression>


//size of NameList has to match the number of return values from the FunctionCall
<MultipleAssignment> -> VAR <NameList> ASSIGN <FunctionCall>


//print statements
<Print> -> PRINT <Expression>


//name and parameter list
<NameList> -> <Name> | <Name> COMMA <NameList>

<ParameterList> -> <Parameter> | <Parameter> COMMA <ParameterList>

<Parameter> -> <Name> | <Expression>


//arithmetic expressions
<Expression> -> <Term> | <Expression> ADD <Term> | <Expression> SUB <Term>

<Term> -> <Factor> | <Term> MULT <Factor> | <Term> DIV <Factor>

<Factor> -> <SubExpression> | <SubExpression> EXP <Factor> | <Value> EXP <Factor> | <Value> | <FunctionCall>

<FunctionCall> -> <Name> LPAREN <ParameterList> RPAREN | <Name> LPAREN <ParameterList> RPAREN COLON <Number>

<SubExpression> -> LPAREN <Expression> RPAREN

<Value> -> <Name> | <Number>


//Literals
<Name> = IDENT

<Number> = NUMBER
